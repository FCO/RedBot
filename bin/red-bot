#!/usr/bin/env perl6

use v6.d;
use Red <red-do>;
use RedBot::Server;

use Whateverable;
use Whateverable::Bits;
use Whateverable::Config;

class Redable does Whateverable {

    method help($msg) {
        “Like this {$msg.server.current-nick}: <some Red code>”;
    }

    red-defaults default => database("SQLite", :database<.connection.db>);

    multi method irc-to-me($_) {
        my $p = run 'docker', 'run',
                '-v', "%*ENV<PWD>/red.db:/code/red.db",
                '--rm',
                'red-bot',
                .Str.substr(4).subst(/"␤"/, "\n", :g),
                :out
        ;
        $p.out.slurp.subst: /\n/,"␤", :g
    }
}

my $*RED-DEBUG = so %*ENV<RED_DEBUG>;
my %*SUB-MAIN-OPTS =
  :named-anywhere,
;

multi MAIN("run") {
    my %*BOT-ENV;
    my $nick = RedBot::Server.^all.map(*.nicks).Seq.flatmap: *.split: " ";

    $CONFIG = %();
    $CONFIG<source>        = 'https://github.com/FCO/RedBot';
    $CONFIG<wiki>          = 'https://github.com/FCO/RedBot/wiki';
    $CONFIG<github>        = %(login => '', access_token => '');
    $CONFIG<irc>           = %(login => '', password => '');
    $CONFIG<default-stdin> = "Red is the ultimate cure for sadness. – Bill Blass\n";
    $CONFIG<cave>          = "#whateverable",
    $CONFIG<caregivers>    = [ "SmokeMachine" ];

    Redable.new.selfrun: $nick, [ ‘red’, fuzzy-nick($nick, 1) ];
}

multi MAIN("list-servers") {
    say %( |RedBot::Server.^all>>.Hash )
}

multi MAIN("create-connection-db") {
    RedBot::Server.^create-table
}

multi MAIN("add-server", Str $name, Str :$host!, Str :$nicks!, Str :$channels!, Str :$alias!) {
    RedBot::Server.^create:
        :$name,
        |(:host($_) with $host),
        |(:nicks($_) with $nicks),
        |(:alias($_) with $alias),
        |(:channels($_) with $channels),
}
